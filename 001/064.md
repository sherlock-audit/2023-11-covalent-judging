Pet Sand Deer

medium

# Unstakings are never deleted leading to addValidatorAddress DOSing permanently

## Summary

When calling OperationalStaking.transferUnstakedOut(), Unstakings are not deleted. Once the validator reaches 300 Unstakings, they will no longer be able to transfer the validator account to a new address because of a validation check in OperationalStaking.setValidatorAddress().

Please note that this issue also can be extended to redelegateUnstaked() and recoverUnstaking() as these two functions also handle potentially empty Unstaking records.

Finally, this report mentions the use of transferUnstakedOut(), which is entirely optional for this exploit to work. However, I want to mention this to emphasis the point that Unstaking records are never deleted.

## Vulnerability Detail

The core of this problem has to deal with two conditions:

- transferUnstakedOut() function does not delete Unstaking records that have been completely depleted
- setValidatorAddress() has a validation check that if more than 300 Unstakings exist for a validator, the function will revert.

These two conditions result in an edge case where setValidatorAddress() will revert after the validator has initiated 300 unstakings, even if they withdrew those unstakings via a call to transferUnstakedOut().

Below is a forge test which represents a scenario where a validator creates more than 300 unstakings during an arbitrary amount of time, transfers the unstakings out, and then decides to transfer the validator to another address. 

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.13;

import "forge-std/Test.sol";
import "../contracts/OperationalStaking.sol";

import "@openzeppelin/contracts/token/ERC20/ERC20.sol";

contract DummyERC20 is ERC20 {
  constructor(string memory name, string memory symbol) ERC20(name, symbol) {}

  function mint(address to, uint256 amount) public {
      _mint(to, amount);
  }
}

// forge test --watch-delay --no-restart -w

contract OperationalStakingTest is Test {
  OperationalStaking operationalStaking;
  DummyERC20 dummyToken;
  address owner = address(0x01);
  address victim = address(0x10);
  address hacker = address(0x66);
  address hacker2 = address(0x67);


  function setUp() public {
    vm.prank(owner);
    dummyToken = new DummyERC20("DummyToken", "DMT");
    operationalStaking = new OperationalStaking();
    
    operationalStaking.initialize(
      address(dummyToken),
      28*6857, // delegatorCoolDown
      180*6857, // validatorCoolDown
      2, // maxCapM
      10e18 // vMaxStake
    );

    operationalStaking.setStakingManagerAddress(owner);
  }


  function testUnstakingsNeverDeleted() public {
    vm.prank(owner);
    operationalStaking.addValidator(victim, 1e17);

    dummyToken.mint(victim, 100e18);
    vm.startPrank(victim);
    dummyToken.approve(address(operationalStaking), 100e18);
    operationalStaking.stake(0, 9e18);

    uint128 unstakeAmount = 10 ** 8;

    for (uint i = 0; i < 301; i++) {
      operationalStaking.unstake(0, unstakeAmount);
    }

    vm.roll(block.number +  180*6857 + 1);

    for (uint i = 0; i < 301; i++) {
      operationalStaking.transferUnstakedOut(unstakeAmount, 0, uint128(i));
    }

    vm.expectRevert("Cannot transfer more than 300 unstakings");
    operationalStaking.setValidatorAddress(0, hacker);
  }
}
```

## Impact

Validators who unstake more than 300 times in a validator will lose the ability to transfer the validator account via calling OperationalStaking.setValidatorAddress().

Because this leads to broken functionality (aka permanent DOS) and the likelihood of this happening is moderate for users who are actively unstaking in their validator account, I would consider this a MEDIUM. 

## Code Snippet

https://github.com/sherlock-audit/2023-11-covalent/blob/main/cqt-staking/contracts/OperationalStaking.sol?plain=1#L700-L702

https://github.com/sherlock-audit/2023-11-covalent/blob/main/cqt-staking/contracts/OperationalStaking.sol?plain=1#L559-L572


## Tool used

Manual Review

## Recommendation

When OperationalStaking.transferUnstakedOut() empties an Unstaking record, that record should be deleted. This should also occur for other functions that deal with potentially emptying unstaking records: redelegateUnstaked() and recoverUnstaking().

Alternatively, the protocol can design external functions that validators may call to "clean up" the unstakings held in that validator. That will allow the validator an opportunity to reduce the number of empty Unstakings held in their account. Note that both options may be gas-intensive so consideration must be given to gas-costs.

Finally, as mentioned in the code documentation the setValidatorAddress() allows users to optionally transfer Unstaking records. That functionality does not exist at all. Adding that functionality gives the validator an opportunity to transfer the Validator account without dealing with the Unstaking record count requirement.
