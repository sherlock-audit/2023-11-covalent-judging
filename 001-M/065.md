Pet Sand Deer

medium

# OperationalStaking.stake() has no slippage checks

## Summary

OperationalStaking.stake() creates shares for users who stake into the validator account. If rewards are distributed just before a user stakes into their account, the exchange rate will increase leading to the user receiving less shares than expected. The user has no way to protect themself from this slippage as there are no slippage checks when calling stake().

## Vulnerability Detail

Consider the following scenario:

1. Delegator wants to stake 9e18 CQT to Validator 1 and calls stake(). The tx is in the mempool.
2. Staking owner distributes rewards of 1e30 to Validator 1 via depositRewardTokens() and this is executed.
3. Delegator's tx from step 1 is executed. Because there are no slippage checks, user's share is much smaller than expected.

To understand why this occurs, let's first look at how shares are calculated in _stake():

```solidity
// Inside _stake()
uint128 sharesAdd = _tokensToShares(amount, v.exchangeRate);

// Inside _tokensToShares()
return uint128((uint256(amount) * DIVIDER) / uint256(rate));
```

As you can see, amount is divided by the exchange rate. When rewardValidators() is called, the exchange rate is increased via the following function:

```solidity
v.exchangeRate += uint128((uint256(amount - commissionPaid) * uint256(DIVIDER)) / v.totalShares);
```

When the exchange rate increases via calling rewardValidators(), the shares formula will lead to less shares minted. Without a slippage check, users will be unable to handle slippage checks.

The following forge test shows this in action. Please note that I added a function to the contract in order to calculate the shares that a user owns as there is no current view function available to review a delegator's shares owed.

Function added to OperationalStaking.sol

```solidity
function getSharesData() public view returns (uint128 shares) {
  Validator storage v = _validators[0];
  // this includes staked + compounded rewards
  shares = _tokensToShares(v.stakings[msg.sender].staked, v.exchangeRate);
}
```

In the forge test below, commenting out the call to rewardValidators() results in the hacker receiving `9000000000000000000` shares. If rewardValidators() is called, the hacker receives `349999986388` shares.

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.13;

import "forge-std/Test.sol";
import "../contracts/OperationalStaking.sol";

import "@openzeppelin/contracts/token/ERC20/ERC20.sol";

contract DummyERC20 is ERC20 {
  constructor(string memory name, string memory symbol) ERC20(name, symbol) {}

  function mint(address to, uint256 amount) public {
      _mint(to, amount);
  }
}

// forge test --watch-delay --no-restart -w

contract OperationalStakingTest is Test {
  OperationalStaking operationalStaking;
  DummyERC20 dummyToken;
  address owner = address(0x01);
  address victim = address(0x10);
  address hacker = address(0x66);
  address hacker2 = address(0x67);


  function setUp() public {
    vm.prank(owner);
    dummyToken = new DummyERC20("DummyToken", "DMT");
    operationalStaking = new OperationalStaking();
    
    operationalStaking.initialize(
      address(dummyToken),
      28*6857, // delegatorCoolDown
      180*6857, // validatorCoolDown
      2, // maxCapM
      35000 * 10 ** 18 // vMaxStake
    );

    operationalStaking.setStakingManagerAddress(owner);
  }
 
  function testRewardValidatorsFrontrun() public {
    vm.startPrank(owner);
    operationalStaking.addValidator(victim, 1e17);

    dummyToken.mint(victim, 35000 * 10 ** 18);
    vm.startPrank(victim);
    dummyToken.approve(address(operationalStaking), 35000 * 10 ** 18);
    operationalStaking.stake(0, 35000 * 10 ** 18);
 
    vm.startPrank(owner);
    operationalStaking.enableValidator(0);
 
    vm.startPrank(operationalStaking.owner());
    dummyToken.mint(operationalStaking.owner(), 1e30);
    dummyToken.approve(address(operationalStaking), 1e30);
    operationalStaking.depositRewardTokens(1e30);

    uint128[] memory ids = new uint128[](1);  
    ids[0] = 0;  
    uint128[] memory amounts = new uint128[](1); 
    amounts[0] = 1e30;  

    // AUDIT: UNCOMMENT TWO LINES BELOW TO SEE HOW MANY SHARES RECEIVE WHEN rewardValidators() IS CALLED
    vm.startPrank(owner);
    operationalStaking.rewardValidators(0, ids, amounts);
 
    dummyToken.mint(hacker, 9e18);
    vm.startPrank(hacker);
    dummyToken.approve(address(operationalStaking), 9e18);
    operationalStaking.stake(0, 9e18);
    
    console.log(operationalStaking.getSharesData());
  }
}
```

## Impact

Users may receive less shares than expected.

## Code Snippet

https://github.com/sherlock-audit/2023-11-covalent/blob/main/cqt-staking/contracts/OperationalStaking.sol?plain=1#L408-L446

https://github.com/sherlock-audit/2023-11-covalent/blob/main/cqt-staking/contracts/OperationalStaking.sol?plain=1?plain=1#L308

## Tool used

Manual Review

## Recommendation

Add a slippage check argument called `minShare` when calling stake(), recoverUnstaking(), and redelegateUnstaked() that will be used in a require statement to ensure that the amount of shares created at least exceed the minShare amount:


```solidity
function _stake(uint128 validatorId, uint128 amount, bool withTransfer, uint128 minShares) internal {
  ...

  uint128 sharesAdd = _tokensToShares(amount, v.exchangeRate);

  // AUDIT: require added to check that the minimum about of shares have been created
  require(sharesAdd >= minShares, "Not enough shares created");
}
```
